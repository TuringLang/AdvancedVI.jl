# AdvancedVI.jl
A library for variational Bayesian inference in Julia.

At the time of writing (05/02/2020), implementations of the variational inference (VI) interface and some algorithms are implemented in [Turing.jl](https://github.com/TuringLang/Turing.jl). The idea is to soon separate the VI functionality in Turing.jl out and into this package.

The purpose of this package will then be to provide a common interface together with implementations of standard algorithms and utilities with the goal of ease of use and the ability for other packages, e.g. Turing.jl, to write a light wrapper around AdvancedVI.jl for integration. 

As an example, in Turing.jl we support automatic differentiation variational inference (ADVI) but really the only piece of code tied into the Turing.jl is the conversion of a `Turing.Model` to a `logjoint(z)` function which computes `z â†¦ log p(x, z)`, with `x` denoting the observations embedded in the `Turing.Model`. As long as this `logjoint(z)` method is compatible with some AD framework, e.g. `ForwardDiff.jl` or `Zygote.jl`, this is all we need from Turing.jl to be able to perform ADVI!

## [WIP] Interface
- `vi`: the main interface to the functionality in this package
  - `vi(model, alg)`: only used when `alg` has a default variational posterior which it will provide.
  - `vi(model, alg, q::VariationalPosterior, Î¸)`: `q` represents the family of variational distributions and `Î¸` is the initial parameters "indexing" the starting distribution. This assumes that there exists an implementation `Variational.update(q, Î¸)` which returns the variational posterior corresponding to parameters `Î¸`.
  - `vi(model, alg, getq::Function, Î¸)`: here `getq(Î¸)` is a function returning a `VariationalPosterior` corresponding to `Î¸`.
- `optimize!(vo, alg::VariationalInference{AD}, q::VariationalPosterior, model::Model, Î¸; optimizer = TruncatedADAGrad())`
- `grad!(vo, alg::VariationalInference, q, model::Model, Î¸, out, args...)`
  - Different combinations of variational objectives (`vo`), VI methods (`alg`), and variational posteriors (`q`) might use different gradient estimators. `grad!` allows us to specify these different behaviors.

## Examples
### Variational Inference
A very simple generative model is the following

    Î¼ ~ ð’©(0, 1)
    xáµ¢ âˆ¼ ð’©(Î¼, 1) , âˆ€i = 1, â€¦, n

where Î¼ and xáµ¢ are some â„áµˆ vectors and ð’© denotes a d-dimensional multivariate Normal distribution.

Given a set of `n` observations `[xâ‚, â€¦, xâ‚™]` we're interested in finding the distribution `p(Î¼âˆ£xâ‚, â€¦, xâ‚™)` over the mean `Î¼`. We can obtain (an approximation to) this distribution that using AdvancedVI.jl!

First we generate some observations and set up the problem:
```julia
julia> using Distributions

julia> d = 2; n = 100;

julia> observations = randn((d, n)); # 100 observations from 2D ð’©(0, 1)

julia> # Define generative model
       #    Î¼ ~ ð’©(0, 1)
       #    xáµ¢ âˆ¼ ð’©(Î¼, 1) , âˆ€i = 1, â€¦, n
       prior(Î¼) = logpdf(MvNormal(ones(d)), Î¼)
prior (generic function with 1 method)

julia> likelihood(x, Î¼) = sum(logpdf(MvNormal(Î¼, ones(d)), x))
likelihood (generic function with 1 method)

julia> logÏ€(Î¼) = likelihood(observations, Î¼) + prior(Î¼)
logÏ€ (generic function with 1 method)

julia> logÏ€(randn(2))  # <= just checking that it works
-311.74132761437653
```
Now there are mainly two different ways of specifying the approximate posterior (and its family). The first is by providing a mapping from distribution parameters to the distribution `Î¸ â†¦ q(â‹…âˆ£Î¸)`:
```julia
julia> using DistributionsAD, AdvancedVI

julia> # Using a function z â†¦ q(â‹…âˆ£z)
       getq(Î¸) = TuringDiagMvNormal(Î¸[1:d], exp.(Î¸[d + 1:4]))
getq (generic function with 1 method)
```
Then we make the choice of algorithm, a subtype of `VariationalInference`, 
```julia
julia> # Perform VI
       advi = ADVI(10, 10_000)
ADVI{AdvancedVI.ForwardDiffAD{40}}(10, 10000)
```
And finally we can perform VI! The usual inferface is to call `vi` which behind the scenes takes care of the optimization and returns the resulting variational posterior:
```julia
julia> q = vi(logÏ€, advi, getq, randn(4))
[ADVI] Optimizing...100% Time: 0:00:01
TuringDiagMvNormal{Array{Float64,1},Array{Float64,1}}(m=[0.16282745378074515, 0.15789310089462574], Ïƒ=[0.09519377533754399, 0.09273176907111745])
```
Let's have a look at the resulting ELBO:
```julia
julia> AdvancedVI.elbo(advi, q, logÏ€, 1000)
-287.7866366886285
```
Unfortunately, the *final* value of the ELBO is not always a very good diagnostic, though the ELBO is an important metric to keep an eye on during training since an *increase* in the ELBO means we're going in the right direction. Luckily, this is such a simple problem that we can indeed obtain a closed form solution! Because we're lazy (at least I am), we'll let [ConjugatePriors.jl](https://github.com/JuliaStats/ConjugatePriors.jl) do this for us:
```julia
julia> # True posterior
       using ConjugatePriors

julia> pri = MvNormal(zeros(2), ones(2));

julia> true_posterior = posterior((pri, pri.Î£), MvNormal, observations)
DiagNormal(
dim: 2
Î¼: [0.1746546592601148, 0.16457110079543008]
Î£: [0.009900990099009901 0.0; 0.0 0.009900990099009901]
)
```
Comparing to our variational approximation, this looks pretty good! Worth noting that in this particular case the variational posterior seems to overestimate the variance.

To conclude, let's make a somewhat pretty picture:
```julia
julia> using Plots

julia> p_samples = rand(true_posterior, 10_000); q_samples = rand(q, 10_000);

julia> p1 = histogram(p_samples[1, :], label="p"); histogram!(q_samples[1, :], alpha=0.7, label="q")

julia> title!(raw"$\mu_1$")

julia> p2 = histogram(p_samples[2, :], label="p"); histogram!(q_samples[2, :], alpha=0.7, label="q")

julia> title!(raw"$\mu_2$")

julia> plot(p1, p2)
```
![Histogram](hist.png?raw=true)

### Simple example: using Advanced.jl to directly minimize the KL-divergence between two distributions `p(z)` and `q(z)`
In VI we aim to approximate the true posterior `p(z âˆ£ x)` by some approximate variational posterior `q(z)` by maximizing the ELBO:

    ELBO(q) = ð”¼_q[log p(x, z) - log q(z)]

Observe that we can express the ELBO as the negative KL-divergence between `p(x, â‹…)` and `q(â‹…)`:

    ELBO(q) = - ð”¼_q[log (q(z) / p(x, z))]
            = - KL(q(â‹…) || p(x, â‹…))

So if we apply VI to something that isn't an actual posterior, i.e. there's no data involved and we write `p(z âˆ£ x) = p(z)`, we're really just minimizing the KL-divergence between the distributions.

Therefore, we can try out `AdvancedVI.jl` real quick by applying using the interface to minimize the KL-divergence between two distributions:

```julia
julia> using Distributions, DistributionsAD, AdvancedVI

julia> # Target distribution
       p = MvNormal(ones(2))
ZeroMeanDiagNormal(
dim: 2
Î¼: [0.0, 0.0]
Î£: [1.0 0.0; 0.0 1.0]
)

julia> logÏ€(z) = logpdf(p, z)
logÏ€ (generic function with 1 method)

julia> # Make a choice of VI algorithm
       advi = ADVI(10, 1000)
ADVI{AdvancedVI.ForwardDiffAD{40}}(10, 1000)
```
Now there are two different ways of specifying the approximate posterior (and its family); the first is by providing a mapping from parameters to distribution `Î¸ â†¦ q(â‹…âˆ£Î¸)`:
```julia
julia> # Using a function z â†¦ q(â‹…âˆ£z)
       getq(Î¸) = TuringDiagMvNormal(Î¸[1:2], exp.(Î¸[3:4]))
getq (generic function with 1 method)

julia> # Perform VI
       q = vi(logÏ€, advi, getq, randn(4))
â”Œ Info: [ADVI] Should only be seen once: optimizer created for Î¸
â””   objectid(Î¸) = 0x5ddb564423896704
[ADVI] Optimizing...100% Time: 0:00:01
TuringDiagMvNormal{Array{Float64,1},Array{Float64,1}}(m=[-0.012691337868985757, -0.0004442434543332919], Ïƒ=[1.0334797673569802, 0.9957355128767893])
```
Or we can check the ELBO (which in this case since, as mentioned, doesn't involve data, is the negative KL-divergence):
```julia
julia> AdvancedVI.elbo(advi, q, logÏ€, 1000)  # empirical estimate
0.08031049170093245
```
It's worth noting that the actual value of the ELBO doesn't really tell us too much about the quality of fit. In this particular case, because we're *directly* minimizing the KL-divergence, we can only say something useful if we reach 0, in which case we have obtained the true distribution.

Let's just quickly check the mean-squared error between the `log p(z)` and `log q(z)` for a random set of samples from the target `p`:
```julia
julia> zs = rand(p, 100);

julia> mean(abs2, logpdf(q, zs) - logpdf(p, zs))
0.0014889109427524852
```
That doesn't look too bad!

## Implementing your own training loop
Sometimes it might be convenient to roll your own training loop rather than using `vi(...)`. Here's some psuedo-code for how one would do that when used together with Turing.jl:

```julia
using Turing, AdvancedVI, DiffResults
using Turing: Variational

using ProgressMeter

# Assuming you have an instance of a Turing model (`model`)

# 1. Create log-joint needed for ELBO evaluation
logÏ€ = Variational.make_logjoint(model)

# 2. Define objective
variational_objective = Variational.ELBO()

# 3. Optimizer
optimizer = Variational.DecayedADAGrad()

# 4. VI-algorithm
alg = ADVI(10, 1000)

# 5. Variational distribution
function getq(Î¸)
    # ...
end

# 6. [OPTIONAL] Implement convergence criterion
function hasconverged(args...)
    # ...
end

# 7. [OPTIONAL] Implement a callback for tracking stats
function callback(args...)
    # ...
end

# 8. Train
converged = false
step = 1

prog = ProgressMeter.Progress(num_steps, 1)

diff_results = DiffResults.GradientResult(Î¸_init)

while (step â‰¤ num_steps) && !converged
    # 1. Compute gradient and objective value; results are stored in `diff_results`
    AdvancedVI.grad!(variational_objective, alg, getq, model, diff_results)

    # 2. Extract gradient from `diff_result`
    âˆ‡ = DiffResults.gradient(diff_result)

    # 3. Apply optimizer, e.g. multiplying by step-size
    Î” = apply!(optimizer, Î¸, âˆ‡)

    # 4. Update parameters
    @. Î¸ = Î¸ - Î”

    # 5. Do whatever analysis you want
    callback(args...)

    # 6. Update
    converged = hasconverged(...) # or something user-defined
    step += 1

    ProgressMeter.next!(prog)
end
```


## References

- Jordan, Michael I., Zoubin Ghahramani, Tommi S. Jaakkola, and Lawrence K. Saul. "An introduction to variational methods for graphical models." Machine learning 37, no. 2 (1999): 183-233.
- Blei, David M., Alp Kucukelbir, and Jon D. McAuliffe. "Variational inference: A review for statisticians." Journal of the American statistical Association 112, no. 518 (2017): 859-877.
- Kucukelbir, Alp, Rajesh Ranganath, Andrew Gelman, and David Blei. "Automatic variational inference in Stan." In Advances in Neural Information Processing Systems, pp. 568-576. 2015.
- Salimans, Tim, and David A. Knowles. "Fixed-form variational posterior approximation through stochastic linear regression." Bayesian Analysis 8, no. 4 (2013): 837-882.
- Beal, Matthew James. Variational algorithms for approximate Bayesian inference. 2003.
